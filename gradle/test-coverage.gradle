apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/reports/coverage")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

def coverageExcludes = ['*.User', '*.UserResource', '*.Application', '*.ResourceNotFoundException', '*.*Dto']

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            includes = ['me.bright.*']
            excludes = coverageExcludes

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }

        rule {
            enabled = true
            element = 'CLASS'
            includes = ['me.bright.*']
            excludes = coverageExcludes

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
}